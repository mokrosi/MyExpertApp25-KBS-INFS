REM <<<<< Fruit.kb >>>>>>>>>>>>>>>>>>>>>>

RULE [Banana]
IF [shape] = "long" and 
[color] :"green" "yellow"
THEN [Fruitclass] ="No class" and
[Seedclass] = "no class" and
[Fruit] ="Banana"

RULE [VineClass]
IF [diameter] > 4 and 
 [shape]: "oblong" "round"
THEN [Fruitclass] ="Vineclass" and
[Seedclass] = "no class"

RULE [TreeClass]
IF [diameter] <=4 and 
 [shape] = "round"
THEN [Fruitclass] ="Treeclass"


REM  <<<<VINE CLASS Fruit>>>>>>>>>>>>>>>>>>


RULE [watermelon]
IF [Fruitclass] ="Vineclass" and
[color] = "green"  
THEN [Fruit] = "Watermelon"


RULE [honeydew]
IF [Fruitclass] ="Vineclass" and 
[color] = "yellow"  and
[surface]  = "smooth"
THEN [Fruit] = "honeydew"

RULE [cantaloupe]
IF [Fruitclass] ="Vineclass" and
[color] = "tan"  and 
[surface]  = "rough"
THEN [Fruit] = "cantaloupe"

REM  <<<<TREE CLASS Fruit>>>>>>>>>>>>>>>>>>


RULE [Stoneclass]
IF [Fruitclass] ="Treeclass"and 
[seedcount]  = "one"
THEN [Seedclass]= "Stoneclass"

RULE [Multipleclass]
IF [Fruitclass] ="Treeclass" and 
[seedcount]  = "many"
THEN [Seedclass]= "Multipleclass"


RULE [peach or apricot]
IF [Seedclass]= "Stoneclass"  and 
[color] = "orange"
THEN [Fruit] = "peach or apricot"


RULE [plum]
IF [Seedclass]= "Stoneclass"  and 
[color] = "purple"
THEN [Fruit] = "plum"

RULE [cherry]
IF [Seedclass]= "Stoneclass"  and 
[color] = "red"
THEN [Fruit] = "cherry"


RULE [Apple]
IF [Seedclass]= "Multipleclass"  and 
[color] : "red" "green" "yellow"
THEN [Fruit] = "Apple"


RULE [orange]
IF [Seedclass]= "Multipleclass"  and 
[color] = "orange"
THEN [Fruit] = "orange"

REM  <<<<Prompts Section >>>>>>>>>>>>>>>>>>
Prompt [shape]  forcedchoice
"What is the shape of the fruit?"
"long"
"oblong"
"round"

Prompt [color]  forcedchoice
"What is the color of the fruit?"
"green"
"yellow"
"tan"
"orange"
"purple"
"red"

Prompt [diameter]  numeric
"What is the diameter of the fruit?"
"0.5"
"10"

Prompt [surface]  forcedchoice
"What is the surface of the fruit?"
"smooth"
"rough"

Prompt [seedcount]  forcedchoice
"What is the seedcount of the fruit?"
"one"
"many"

Goal [Fruit]
Goal [Seedclass]
Goal [Fruitclass]

DEFAULT [Fruit] = "there is no fruit with his/her description"